! Morphological Transducer for aii

Multichar_Symbols

! Features
%[%-hab%] !  habitual - verb picks this tag up in LEXICON Prefixes
%[%+hab%] !  if verb picks this tag up later, will analyze with <hab>
%[%-fut%] !  future on verbs
%[%+fut%]
%[%-obl%] ! oblique on Pronouns
%[%+obl%]

! Part of speech categories
%<n%>   ! Noun
%<vblex%>   ! Verb (lexical)
%<adj%>   ! Adjective
%<prn%>   ! Pronoun
%<adv%>   ! Adverb
%<pr%>   ! Preposition

! Archiphonemes
%{A%}   ! > Alaf if word-final or word-initial, otherwise null

! Person morphology
%<p1%> ! First Person
%<p2%> ! Second Person
%<p3%> ! Third Person

! Gender morphology
%<m%> ! Masculine
%<f%> ! Feminine

! Number morphology
%<pl%>  ! Plural
%<sg%>  ! Singular

! Gender morphology
%<m%> ! Masculine
%<f%> ! Feminine

! Person
%<p1%> ! First Person
%<p2%> ! Second Person
%<p3%> ! Third Person

! Case morphology
%<nom%> ! Nominative
%<gen%> ! Genitive

! TAM morphology
%<hab%> ! Habitual
%<past%> ! Past
%<pres%> ! Present
%<pres%> ! Pres
%<fut%> ! Future

! Pronoun morphology
%<obl%> ! Oblique

! Other symbols
%>      ! Morpheme boundary

! Punctuation
%<sent%> ! Sentence punct
%<guio%> ! Dashes
%<cm%> ! Comma
%<apos%> ! Apostrophe
%<equot%> ! End quote
%<bquot%> ! Begin quote
%<epar%> ! End paragraph
%<bpar%> ! Begin paragraph


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Prefix ;
VerbRoot ;
NounRoot ;
PronounRoot ;
AdverbRoot ;
AdjectiveRoot ;
PrepositionRoot ;
Punctuation ;

LEXICON Prefix

VerbPrefix ;
PronounPrefix ;

LEXICON VerbPrefix

%[%-fut%]:ܒܹܬ VerbRoot ;
%[%-hab%]:ܟܝܼ VerbRoot ;

LEXICON PronounPrefix

%[%-obl%]:ܕ PronounRoot ;

LEXICON RegNounInfl

%<n%>: NounInfl-Number ;

LEXICON NounInfl-Number

%<sg%>:◌ܵ%{A%} NounInfl-CaseNom ;

%<sg%>: NounInfl-CaseGen;

%<pl%>:◌ܹ%{A%} NounInfl-CaseNom ;

LEXICON NounInfl-CaseNom

%<nom%>: # ;

LEXICON NounInfl-CaseGen

%<gen%>:◌ܹܬ # ;

LEXICON PresentVerbInfl

%<vblex%>%<pres%>: PresentVerbInfl-Suffix ;

LEXICON PresentVerbInfl-Suffix

%<p3%>%<m%>%<sg%>: PresentVerbInfl-Prefix ;
%<p3%>%<f%>%<sg%>:◌ܵܐ PresentVerbInfl-Prefix ;
%<p3%>%<pl%>:ܝܼ PresentVerbInfl-Prefix ;
%<p2%>%<m%>%<sg%>:◌ܹܬ PresentVerbInfl-Prefix ;
%<p2%>%<f%>%<sg%>:◌ܵܐ PresentVerbInfl-Prefix ;
%<p2%>%<pl%>:ܝܼܬܘܼܢ PresentVerbInfl-Prefix ;
%<p1%>%<m%>%<sg%>:◌ܹܢ PresentVerbInfl-Prefix ;
%<p1%>%<f%>%<sg%>:◌ܵܢ PresentVerbInfl-Prefix ;
%<p1%>%<pl%>:◌ܵܚ PresentVerbInfl-Prefix ;

! long form -- should analyze but not generate

%<p3%>%<m%>%<sg%>:ܢܝܼ PresentVerbInfl-Prefix ; ! Dir/LR
%<p3%>%<f%>%<sg%>:◌ܵܢܝܼ PresentVerbInfl-Prefix ; ! Dir/LR
%<p3%>%<pl%>:ܝܼܢܝܼ PresentVerbInfl-Prefix ; ! Dir/LR
%<p2%>%<m%>%<sg%>:ܝܼܬܹܢ PresentVerbInfl-Prefix ; ! Dir/LR
%<p2%>%<f%>%<sg%>:◌ܵܬܹܢ PresentVerbInfl-Prefix ; ! Dir/LR
%<p2%>%<pl%>:ܝܼܬܘܼܢ PresentVerbInfl-Prefix ; ! Dir/LR
%<p1%>%<m%>%<sg%>:ܝܼܢܵܐ PresentVerbInfl-Prefix ; ! Dir/LR
%<p1%>%<f%>%<sg%>:◌ܵܢܵܐ PresentVerbInfl-Prefix ; ! Dir/LR
%<p1%>%<pl%>:◌ܵܚܹܢ PresentVerbInfl-Prefix ; ! Dir/LR

! This was the old way of doing prefixes--replaced with PresentVerbInfl-Prefix
!  ||
!  \/

! Paths with hab prefix

! %<p3%>%<m%>%<sg%>%<hab%>%[%+hab%]:◌ܹܚ ;
! %<p3%>%<f%>%<sg%>%<hab%>%[%+hab%]:ܚܵܐ ;
! %<p3%>%<pl%>%<hab%>%[%+hab%]:ܚܝܼ # ;
! %<p2%>%<m%>%<sg%>%<hab%>%[%+hab%]:ܚܹܬ # ;
! %<p2%>%<f%>%<sg%>%<hab%>%[%+hab%]:ܚܵ # ;
! %<p2%>%<pl%>%<hab%>%[%+hab%]:ܚܝܼܬܘܼܢ # ;
! %<p1%>%<m%>%<sg%>%<hab%>%[%+hab%]:ܚܹܢ # ;
! %<p1%>%<m%>%<sg%>%<hab%>%[%+hab%]:ܚܵܢ # ;
! %<p1%>%<pl%>%<hab%>%[%+hab%]:ܚܵܚ # ;

! Paths with fut prefix

! %<p3%>%<m%>%<sg%>%<fut%>%[%+fut%]:◌ܹܚ # ;
! %<p3%>%<f%>%<sg%>%<fut%>%[%+fut%]:ܚܵܐ # ;
! %<p3%>%<pl%>%<fut%>%[%+fut%]:ܚܝܼ # ;
! %<p2%>%<m%>%<sg%>%<fut%>%[%+fut%]:ܚܹܬ # ;
! %<p2%>%<f%>%<sg%>%<fut%>%[%+fut%]:ܚܵ # ;
! %<p2%>%<pl%>%<fut%>%[%+fut%]:ܚܝܼܬܘܼܢ # ;
! %<p1%>%<m%>%<sg%>%<fut%>%[%+fut%]:ܚܹܢ # ;
! %<p1%>%<m%>%<sg%>%<fut%>%[%+fut%]:ܚܵܢ # ;
! %<p1%>%<pl%>%<fut%>%[%+fut%]:ܚܵܚ # ;

LEXICON PresentVerbInfl-Prefix

%<fut%>%[%+fut%]: # ;
%<hab%>%[%+hab%]: # ;
: # ;

LEXICON PastVerbInfl

%<vblex%>%<past%>: PastVerbInfl-Suffix ;

LEXICON PastVerbInfl-Suffix

%<p3%>%<m%>%<sg%>:ܠܹܐ # ;
%<p3%>%<f%>%<sg%>:ܠܵܐ # ;
%<p3%>%<pl%>:ܠܘܼܢ # ;
%<p2%>%<m%>%<sg%>:ܠܘܼܚ # ;
%<p2%>%<f%>%<sg%>:ܠܵܚ # ;
%<p2%>%<pl%>:ܠܿܚܘܼܢ # ;
%<p1%>%<sg%>:ܠܝܼ # ;
%<p1%>%<pl%>:ܠܵܢ # ;

LEXICON PronounInfl-SgMNear

%<prn%>%<sg%>%<m%>%<near%>: PronounInfl-Prefix ;

LEXICON PronounInfl-PlNear

%<prn%>%<pl%>%<near%>: PronounInfl-Prefix ;

LEXICON PronounInfl-SgMMed

%<prn%>%<sg%>%<m%>%<med%>: PronounInfl-Prefix ;

LEXICON PronounInfl-SgFMed

%<prn%>%<sg%>%<f%>%<med%>: PronounInfl-Prefix ;

LEXICON PronounInfl-SgMFar

%<prn%>%<sg%>%<m%>%<far%>: PronounInfl-Prefix ;

LEXICON PronounInfl-SgFFar

%<prn%>%<sg%>%<f%>%<far%>: PronounInfl-Prefix ;

LEXICON PronounInfl-PlFar

%<prn%>%<pl%>%<far%>: PronounInfl-Prefix ;

LEXICON PronounInfl-SgMDef

%<prn%>%<sg%>%<m%>%<def%>: PronounInfl-Prefix ;

LEXICON PronounInfl-SgFDef

%<prn%>%<sg%>%<f%>%<def%>: PronounInfl-Prefix ;

LEXICON PronounInfl-PlDef

%<prn%>%<pl%>%<def%>: PronounInfl-Prefix ;

LEXICON PronounInfl-Prefix

%<obl%>%[%+obl%]: # ;
: # ;

LEXICON AdverbInfl

%<adv%>: # ;

LEXICON AdjectiveInfl

%<adj%>: # ;

LEXICON PrepositionInfl

%<pr%>: PrepositionInfl-Attachments ;

LEXICON PrepositionInfl-Attachments

: # ;
%<enc%>:◌ܹܬ # ;
%<gen%>: ܕܝܼܝܝ PrepositionInfl-Genitive ;

LEXICON PrepositionInfl-Genitive

%<p3%>%<f%>%<sg%>:◌ܿܐ # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

ܡܵܠܟ:ܡܵܠܟ RegNounInfl ; ! malc- "king"
ܬܿܪ:ܬܿܪ RegNounInfl ; ! tor- "ox"
ܣܵܕ:ܣܵܕ RegNounInfl ; ! sad- "witness"
ܚܡܵܪ:ܚܡܵܪ RegNounInfl ; ! xmar- "ass"
ܢܘܼܝܢ:ܢܘܼܝܢ RegNounInfl ; ! nuny- "fish"
ܦܬܵܢ:ܦܬܵܢ RegNounInfl ; ! ptan- "plough"
ܬܘܼܝܬ:ܬܘܼܝܬ RegNounInfl ; ! tuyt- "mulberry"
ܬܸܢ:ܬܸܢ RegNounInfl ; ! ten- "fig"
ܫܵܕ:ܫܵܕ RegNounInfl ; ! shad- "almond"
ܒܸܬ:ܒܸܬ RegNounInfl ; ! bet- "house"
ܟܵܢܬܘܼܢܬ:ܟܵܢܬܘܼܢܬ RegNounInfl ; ! kantunt- "cat"
ܝܼܣܵܟܬ:ܝܼܣܵܟܬ RegNounInfl ; ! isakt- "ring"
ܟܿܡܘܼܪ:ܟܿܡܘܼܪ RegNounInfl ; ! comur- "coal"
ܒܵܒ:ܒܵܒ RegNounInfl ; ! bab- "father"
ܒܸܪܟ:ܒܸܪܟ RegNounInfl ; ! berc- "knee"
ܒܝܼܬ:ܒܝܼܬ RegNounInfl ; ! bit- "egg"
ܙܵܡܵܪ:ܙܵܡܵܪ  RegNounInfl ; ! zamar- "singer"
ܡܵܬ:ܡܵܬ RegNounInfl ; ! mat- "village"
ܚܕ:ܚܕ RegNounInfl ; ! 'had- "a, an, one"

LEXICON VerbRoot

ܦܵܬܚ:ܦܵܬܚ PresentVerbInfl ; ! patx- "to open (present template)"
ܦܬܹܚ:ܦܬܹܚ PastVerbInfl ; ! ptəx- "to open (past template)"
ܐܵܬܹܐ:%{A%}◌ܵܬܹ%{A%} PresentVerbInfl; ! atə "to come"
ܩܵܛܠ:ܩܵܛܠ PresentVerbInfl ; ! katəl "to kill"
ܡܵܕܡܹܚ:ܡܵܕܡܹܚ PresentVerbInfl ; ! madməx "to cause to sleep"
ܡܘܼܠܸܕ:ܡܘܼܠܸܕ PresentVerbInfl; ! molidh "to begat, to conceive"

LEXICON PronounRoot

ܐܵܗܵܐ:%{A%}◌ܵܗܵ%{A%} PronounInfl-SgMNear ; ! there's actually a vowel [a] over ◌ that doesn't render right in some text editors
ܐܵܢܢܹܐ:%{A%}◌ܵܢܢܹ%{A%} PronounInfl-PlNear ;
ܐܵܒܒܵܐ:%{A%}◌ܵܒܒܵ%{A%} PronounInfl-SgMMed ;
ܐܵܝܝܵܐ:%{A%}◌ܵܝܝܵ%{A%} PronounInfl-SgFMed ;
ܐܵܒܒܿܐ:%{A%}◌ܵܒܒܿ%{A%} PronounInfl-SgMFar ;
ܐܵܝܝܸܐ:%{A%}◌ܵܝܝܸ%{A%} PronounInfl-SgFFar ;
ܐܵܒܒܝܼ:%{A%}◌ܵܒܒܝܼ PronounInfl-PlFar ;
ܐܵܒܘܼܢ:%{A%}◌ܵܒܘܼܢ PronounInfl-SgMDef ;
ܐܵܝܹܢ:%{A%}◌ܵܝܹܢ PronounInfl-SgFDef ;
ܐܵܢܝܼ:%{A%}◌ܵܢܝܼ PronounInfl-PlDef ;

LEXICON AdverbRoot

ܢܝܼܚܵܐ:ܢܝܼܚܵܐ AdverbInfl ; ! nixa 'slowly'
ܚܹܪܒܵܐ:ܚܹܪܒܵܐ AdverbInfl ; ! xərba 'badly'

LEXICON AdjectiveRoot
ܚܵܬܹܐ:ܚܵܬܹܐ AdjectiveInfl ;
ܫܵܚܝܼܢܵܐ:ܫܵܚܝܼܢܵܐ AdjectiveInfl ;
ܟܠ:ܟܠ AdjectiveInfl ; ! had 'any, all'

LEXICON Punctuation

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%؛%<sent%>:%؛ # ;
! %%<sent%>:%: # ; I can't figure out how to type the colon
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
%،%<cm%>:%، # ;
%؟%<sent%>:%؟ # ;
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<equot%>:%« # ; ! changed from lquot to equot "end quote" because that's confusing
%»%<bquot%>:%» # ;
%”%<equot%>:%” # ;
%“%<bquot%>:%“ # ;
%(%<epar%>:%( # ;
%]%<bpar%>:%] # ;
%[%<epar%>:%[ # ;
%)%<bpar%>:%) # ;
%܀%<epar%>:%܀ # ; ! special Syriac paragraph ending symbol
\%<sent%>:\ # ;
/%<sent%>:/ # ;

LEXICON PrepositionRoot

ܠܹܗ:ܠܹܗ PrepositionInfl ; ! leh object marker
ܐܹܠܬܹܚ:ܐܹܠܬܹܚ PrepositionInfl ; ! eltex 'underneath' (I don't feel like typing the schwas)
ܘܼܠܠܘܼܠ:ܘܼܠܠܘܼܠ PrepositionInfl ; ! ullul 'above'
ܕܠܵܐ:ܕܠܵܐ PrepositionInfl ; ! d-la 'without'

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst
